buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'
        classpath "org.jetbrains.dokka:dokka-android-gradle-plugin:0.9.16"
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'org.jetbrains.dokka-android'

apply from: 'versioning.gradle'

group = 'io.clappr.player'
version = '0.21.1'

def publishAttrs = buildPublishAttrs(version)

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    configurations = ['archives']
    pkg {
        publish = true
        repo = 'clappr'
        name = publishAttrs['bintrayRepoName']
        userOrg = 'clappr-android'
        licenses = ['BSD 3-Clause']
        vcsUrl = 'https://github.com/clappr/clappr-android.git'
        version {
            name = publishAttrs['versionName']
            desc = publishAttrs['bintrayVersionDesc']
            released  = new Date()
            vcsTag = publishAttrs['versionName']
        }
    }
}

android {
    publishNonDefault publishAttrs['bintrayPublishNonDefault']
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28

        buildConfigField("String", "CLAPPR_VERSION", "\"$version\"")
    }
    packagingOptions {
        // workaround for http://stackoverflow.com/questions/20673625/android-gradle-plugin-0-7-0-duplicate-files-during-packaging-of-apk
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }
    // ignore lint temporarily until gocd can handle the report correctly
    lintOptions {
        abortOnError false
    }

    testOptions {
        unitTests.all {
            jvmArgs '-XX:MaxPermSize=256m'
        }
        unitTests {
            includeAndroidResources = true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

configurations {
    debug
    release
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.android.support:appcompat-v7:$android_support_version"
    implementation "com.android.support:support-v4:$android_support_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'com.google.android.exoplayer:exoplayer:2.9.2'

    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.0.2'

    testImplementation "junit:junit:4.12"
    testImplementation "org.robolectric:robolectric:4.3"
    testImplementation 'org.khronos:opengl-api:gl1.1-android-2.1_r1'
    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testImplementation "com.nhaarman:mockito-kotlin:1.5.0"
    testImplementation 'androidx.test:core:1.2.0'
}

tasks.matching {it instanceof Test}.all {
    testLogging.events = ["failed", "passed", "skipped"]
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

task deleteDocsFolder(type: Delete) { delete 'doc/clappr/' }

dokka {
    dependsOn deleteDocsFolder

    outputFormat = 'gfm'
    outputDirectory = "doc"
    processConfigurations = []

    linkMapping {
        dir = "src/main/kotlin"
        url = "https://github.com/clappr/clappr-android/tree/dev/clappr/src/main/kotlin"
        suffix = "#L"
    }
}

